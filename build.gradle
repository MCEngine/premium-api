plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

// Set project version using a property
version = project.properties[project.name + '.version']
group = 'io.github.mcengine'

ext {
    // User
    user_git_name = System.getenv('USER_GIT_NAME')
    user_github_token = System.getenv('USER_GITHUB_TOKEN')

    // Organization
    organization_name = System.getenv('ORGANIZATION_NAME') ?: 'MCEngine'
    organization_token = System.getenv('ORGANIZATION_TOKEN')

    // Repository
    repository_name = System.getenv('REPOSITORY_NAME') ?: 'premium-api'
    repository_token = System.getenv('REPOSITORY_TOKEN')

    // Project Information
    jarName = System.getenv('PROJECT_NAME') ?: 'MCEnginePremium'
}

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }

    maven {
        url = uri('https://maven.pkg.github.com/MCEngine/core-api')
        credentials {
            username = project.user_git_name
            password = project.user_github_token ?: project.repository_token ?: project.organization_token
        }
    }
}

dependencies {
    compileOnly 'com.google.code.gson:gson:2.13.1'
    compileOnly 'org.spigotmc:spigot-api:1.21.7-R0.1-SNAPSHOT'
}

shadowJar {
    version = version
    archiveClassifier = null
    archiveBaseName = project.jarName
}

tasks.named('jar').configure {
    enabled = false
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/${project.organization_name}/${project.repository_name}")
            credentials {
                username = project.user_git_name
                password = project.user_github_token ?: project.repository_token ?: project.organization_token
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifact tasks.named('shadowJar').get()
            artifactId = 'mcengine-premium-api'

            pom {
                name.set(project.jarName)
                description.set("The ${project.jarName} API is designed for the Premium system.")
                url.set("https://github.com/${project.organization_name}/${project.repository_name}")

                scm {
                    connection.set("scm:git:git://github.com/${project.organization_name}/${project.repository_name}.git")
                    developerConnection.set("scm:git:ssh://github.com:${project.organization_name}/${project.repository_name}.git")
                    url.set("https://github.com/${project.organization_name}/${project.repository_name}")
                }

                developers {
                    developer {
                        id.set('jetsadawijit')
                        name.set('Jetsada Wijit')
                        email.set('jetsadawijit@outlook.com')
                        url.set('https://jetsadawijit.github.io')
                    }
                }
            }
        }
    }
}

build.dependsOn(shadowJar)
